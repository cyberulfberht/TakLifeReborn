// Script written by Armitxes
_uniforms = [
  ["unUni","UN_CDF_Soldier_EP1"],
  ["unUni2","UN_CDF_Soldier_Officer_EP1"],
  ["unUni3","UN_CDF_Soldier_Light_EP1"],
  ["unUni4","UN_CDF_Soldier_Pilot_EP1"],
  ["cc","TK_Commander_EP1"],
  ["CopUniDef","Policeman"],
  ["copUni","Soldier_Crew_PMC",[0,"images\jobs\cop_traffic.paa"]],
  ["copUni1","Soldier_Crew_PMC",[0,"images\jobs\cop_cr.paa"]],
  ["copUni2","BAF_Soldier_Marksman_MTP",[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni3","BAF_Soldier_L_MTP",[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni4","BAF_crewman_MTP",[0,"images\jobs\cop_sco_cloth.paa"],[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni5","BAF_Soldier_L_MTP",[0,"images\jobs\cop_sco_cloth.paa"],[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni6","BAF_Pilot_MTP",[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni7","BAF_Pilot_MTP",[0,"images\jobs\cop_sco_cloth.paa"],[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni8","Soldier_Crew_PMC",[0,"images\jobs\cop_sgt.paa"]],
  ["copUni9","Soldier_Crew_PMC",[0,"images\jobs\cop_interceptor.paa"]],
  ["copUni10","Soldier_Crew_PMC",[0,"images\jobs\cop_mp.paa"]],
  ["copUni11","BAF_crewman_MTP",[0,"images\jobs\cop_sco_cloth_grey.paa"],[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["copUni12","BAF_Soldier_L_MTP",[0,"images\jobs\cop_sco_cloth_grey.paa"],[1,"images\jobs\cop_sco_vest.paa"],[2,"images\jobs\cop_sco_equip.paa"]],
  ["civUni1","TK_CIV_Takistani01_EP1"],
  ["civUni2","TK_CIV_Takistani02_EP1"],
  ["civUni3","TK_CIV_Takistani03_EP1"],
  ["civUni4","TK_CIV_Takistani04_EP1"],
  ["civUni5","TK_CIV_Worker01_EP1"],
  ["civUni6","Pilot_EP1"],
  ["civUni7","Functionary2_EP1"],
  ["civUni8","Functionary1_EP1"],
  ["civUni9","SchoolTeacher",[0,"images\jobs\judge.paa"]],
  ["lawyer","RU_Functionary2"],
  ["docUni","Dr_Hladik_EP1"],
  ["docUni2","USMC_LHD_Crew_Blue"],
  ["docUni3","Doctor"],
  ["docUni4","Worker2",[0,"images\jobs\paramedic.paa"]],
  ["TerrorUni1","TK_GUE_Soldier_3_EP1"],        // Terrorist (Bandana)
  ["TerrorUni2","TK_GUE_Soldier_5_EP1"],        // Terrorist (Turban)
  ["TerrorUni3","TK_GUE_Soldier_AA_EP1"],       // Terrorist (Brown)
  ["TerrorUni4","TK_GUE_Soldier_AR_EP1"],       // Terrorist (Grey)
  ["TerrorUni5","TK_GUE_Soldier_MG_EP1"],       // MachineGunner
  ["TerrorUni6","TK_GUE_Soldier_Sniper_EP1"],   // Scout
  ["TerrorUni7","TK_GUE_Warlord_EP1"],          // Warlord
  ["SouthUni1","TK_Soldier_EP1"],               // Soldier
  ["SouthUni2","TK_Soldier_B_EP1"],             // Soldier (Backpack)
  ["SouthUni3","TK_Soldier_Crew_EP1"],          // Soldier (Crew)
  ["SouthUni4","TK_Soldier_Officer_EP1"],       // Commander
  ["SouthUni5","TK_Soldier_Pilot_EP1"],         // Pilot
  ["SouthUni6","TK_Soldier_Sniper_EP1"],        // Sniper
  ["SouthUni7","TK_Special_Forces_EP1"],        // Special Forces
  ["DonUni1","RU_Profiteer1"],                  // Profiteer
  ["DonUni2","RU_Villager2"],                   // Villager
  ["DonUni3","RU_Worker3"],                     // Worker
  ["DonUni4","Ins_Woodlander1"],                // Woodlander
  ["DonUni5","TK_INS_Soldier_AAT_EP1"],          // Rebel
  ["DonUni6","INS_Worker2"], 					// local
  ["DonUni7","INS_Bardak"], 					//Bardak
  ["DonUni8","INS_Lopotev"], 					//Lopotev
  ["PmcUni1","Soldier_TL_PMC"],					// Team Coordinator
  ["PmcUni2","Soldier_Bodyguard_AA12_PMC"],		// Bodyguard
  ["PmcUni3","Soldier_Sniper_PMC"],				// Defensive Marksman
  ["PmcUni4","Soldier_Medic_PMC"],				// PMC Operative
  ["PmcUni5","Soldier_MG_PKM_PMC"],			    // Field Specialist
  ["PmcUni6","Soldier_GL_M16A2_PMC"],			// Security Contractor
  ["PmcUni7","Soldier_Pilot_PMC"],				// PMC Pilot
  ["PmcUni8","Ry_PMC"],						   // Team Leader
  ["GangUni1","GUE_Soldier_MG"],				// Hood
  ["GangUni2","GUE_Soldier_GL"],               //Balaclava
  ["GangUni3","GUE_Soldier_CO"],			   // Cap
  ["GangUni4","GUE_Soldier_AR"],			   // Boonie
  ["GangUni5","GUE_Commander"],				   // Commander
  ["GangUni6","GUE_Soldier_Pilot"],			  // Gang Pilot
  ["northWar","US_Soldier_EP1"]			  // North War
];

_medics = ["docUni","docUni2","docUni3","docUni4"];
_pmcs = ["PmcUni1","PmcUni2","PmcUni3","PmcUni4","PmcUni5","PmcUni6","PmcUni7","PmcUni8"];


_action = (_this select 0);
if(_action == "use") exitWith {
	if((vehicle player) != player) exitWith { hint "You can't change your clothes while in a vehicle"; };
	if(warStarted) exitWith { hint "You can't change your clothing during a war!"; };
	{
		_type = (_x select 0);
		if (_type == (_this select 1)) exitWith {
			if (_type != "northWar") then { previousPlayerType = _type };
			if(typeOf player == (_x select 1)) exitWith { };
			disableUserInput true;
			_pos = getPos player;
			_old = player;
			_grp = group player;
			_warrarr = [_old] call fnc_getWarrant;
			[(_warrarr select 1)] call fnc_removeWarrant;

			titleText ["Changing clothes...", "BLACK FADED"];

			_wep = ((weapons _old) - (items _old));
			_pwep = primaryWeapon _old;
			_mag = magazines _old;

			_newUnit = _grp createUnit [(_x select 1), _pos, [], 0, "NONE"];
			addSwitchableUnit _newUnit;

			sleep 0.5;
			if (!isnil ("_newUnit")) then {
				if(_type in _medics) then { ismedic = true; } else { ismedic = false; };
				if(_type in _pmcs) then { ispmc = true; } else { ispmc = false; };
				if(_type == "civUni9") then { isjudge = true; } else { isjudge = false; };
				selectPlayer _newUnit;
				_pubPlrData = [PLAYERDATA select 4, PLAYERDATA select 5, PLAYERDATA select 9,0];
				_plrInit = format['this setVehicleVarName "%1"; %1 = this; this setVariable ["pubPlrData",%2]; ', rolestring, _pubPlrData];
				if(count _x == 3) then {
					_plrInit = _plrInit + format["this setObjectTexture %1;", (_x select 2)];
					respawnSkin = (_x select 2);
				};
				if(count _x == 4) then {
					_plrInit = _plrInit + format["this setObjectTexture %1;this setObjectTexture %2", (_x select 2), (_x select 3)];
					respawnSkin = (_x select 2); respawnSkinb = (_x select 3);
				};
				if(count _x == 5) then {
					_plrInit = _plrInit + format["this setObjectTexture %1;this setObjectTexture %2;this setObjectTexture %3",(_x select 2),(_x select 3),(_X select 4)];
					respawnSkin = (_x select 2); respawnSkinb = (_x select 3); respawnSkinc = (_x select 4);
				};
				player setVehicleInit _plrInit;
				processInitCommands;
				if(_old isKindOf "Animal") then { deleteVehicle playerPet; playerPet = nil; };
				deleteVehicle _old;
				sleep 0.2;

				{
					if (_x == _old) exitWith { call compile format ["%1 = player; publicVariable str(%1);",rolestring]; };
				} foreach playerarray;

				removeAllWeapons _newUnit;
				{_newUnit addMagazine _x} forEach _mag;
				{_newUnit addWeapon _x} forEach _wep;
				_newUnit selectWeapon _pwep;

				[(_x select 0), -1] call INV_AddInventoryItem;
				player addEventHandler ["fired", {["fired", (_this select 4), (_this select 1)] execVM "stun.sqf";}];
				player addEventHandler ["handleDamage", {_this call compile preprocessfile "armitxes\setHit.sqf"}];
				_warrant = _warrarr select 0;
				if !(count _warrant == 0) then {
					_warrant set [0,player];
					warrantarray = warrantarray + [_warrant];
					publicVariable "warrantarray";
				};
			};
			titleText ["Clothes changed", "BLACK IN"];
			disableUserInput false;
		};
	} forEach _uniforms;
};

if(_action == "respawn" && !(isNil "respawnSkin")) then {
	_plrInit = "";
	_plrInit = _plrInit + format["this setObjectTexture %1;",respawnSkin];
	if ( !(isNil "respawnSkinb") ) then {
		_plrInit = _plrInit + format["this setObjectTexture %1;",respawnSkinb];
		if ( !(isNil "respawnSkinc") ) then {
			_plrInit = _plrInit + format["this setObjectTexture %1;",respawnSkinc];
		}
	};
	player setVehicleInit _plrInit;
	processInitCommands;
};
